plugins {
    id 'com.android.application'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.wanris.xxshop"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // ARoute路由
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        flavorDimensions "versionCode"
    }

    // 根据渠道版本号自动生成包名
    applicationVariants.all {
        variant ->
            variant.outputs.all {
                //服务地址类型
                def serverUrlType = rootProject.ext.SERVER_CONFIG.SERVER_URL_TYPE
                outputFileName = "xxshop"
                def useReactNative = rootProject.ext.SERVER_CONFIG.USE_REACT_NATIVE.toBoolean()
                outputFileName += "_" + defaultConfig.versionName
                outputFileName += "_" + defaultConfig.versionCode
                outputFileName += useReactNative ? "_rn" : ""
                outputFileName += "_${variant.productFlavors[0].name}"
                outputFileName += "_" + ((serverUrlType == 0) ? "prod" :"test")
                outputFileName += "_" + buildType.name
                outputFileName += "_" + new Date().format("yyyy-MM-dd_HHmm")
                outputFileName += ".apk"
            }
    }

    // 多渠道包
    productFlavors {
        // 小米
        xm {}
        // 华为
        hw {}
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    // 集成React Native
    implementation "com.facebook.react:react-native:0.63.5" // From node_modules
    implementation "org.webkit:android-jsc:+"
    // ARoute路由
    implementation ("com.alibaba:arouter-api:$versions_arouter_api") {
        exclude group: 'com.android.support'
    }
    annotationProcessor "com.alibaba:arouter-compiler:$versions_arouter_compiler"

    implementation project(':module-home')
    implementation project(':business-common')
    implementation project(':library-basic')

    // glide图片加载
//    xmImplementation "com.github.bumptech.glide:glide:$versions_glide"
//    xmAnnotationProcessor "com.github.bumptech.glide:compiler:$versions_glide"
}

// 集成React Native
apply from: file("../../node_modules/@react-native-community/cli-platform-android/native_modules.gradle"); applyNativeModulesAppBuildGradle(project)